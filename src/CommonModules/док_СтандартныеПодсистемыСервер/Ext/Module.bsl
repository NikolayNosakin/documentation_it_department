
Процедура УстановкаПараметровСеанса() Экспорт
		
	Если Пользователи.ЭтоПолноправныйПользователь(,,Ложь) Тогда
		ПараметрыСеанса.док_ИспользоватьОграничениеRLSПоКлиентам = Ложь;
	Иначе
		Если ПолучитьФункциональнуюОпцию("док_ИспользоватьОграничениеRLS_ПоКлиентам") Тогда
			Пользователь = Пользователи.ТекущийПользователь();
			ПараметрыСеанса.док_ИспользоватьОграничениеRLSПоКлиентам = ИспользоватьОграничениеRLS_ПоКлиентам(Пользователь);
			ПараметрыСеанса.док_РазрешенныеКлиенты = Новый ФиксированныйМассив(ПолучитьРазрешенныхКлиентовПользователя(Пользователь)); 
		КонецЕсли;
	КонецЕсли;
	
	#Если НЕ ВнешнееСоединение Тогда
		ТекущийСтиль = док_ОбщегоНазначения.ПолучитьНастройкуПользователя(Пользователи.ТекущийПользователь(),"Стиль");			
		Если НЕ ТекущийСтиль = "" Тогда
			Попытка
				ГлавныйСтиль = БиблиотекаСтилей[ТекущийСтиль];
			Исключение
				ПодробнаяИнформацияОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ТекстОшибки = СтрШаблон("Не удалось установить главный стиль ""%1"" по причине:
				|%2",
				ТекущийСтиль,
				ПодробнаяИнформацияОбОшибке);
				ЗаписьЖурналаРегистрации(
				"Установка главного стиля приложения",
				УровеньЖурналаРегистрации.Предупреждение,
				,,
				ТекстОшибки);
			КонецПопытки;	 
		КонецЕсли;
		
		ИмяПользователя = ИмяПользователя();	
		МасштабФорм = док_ОбщегоНазначения.ПолучитьНастройкуПользователя(Пользователи.ТекущийПользователь(),"МасштабФорм");
		Если НЕ МасштабФорм = "" Тогда 
			НастройкиКлиентскогоПриложения = ХранилищеСистемныхНастроек.Загрузить("Общее/НастройкиКлиентскогоПриложения",,,ИмяПользователя);
			Если НЕ НастройкиКлиентскогоПриложения = Неопределено Тогда
				НастройкиКлиентскогоПриложения.ВариантМасштабаФормКлиентскогоПриложения = ВариантМасштабаФормКлиентскогоПриложения[МасштабФорм];
				ХранилищеСистемныхНастроек.Сохранить("Общее/НастройкиКлиентскогоПриложения", "", НастройкиКлиентскогоПриложения, , ИмяПользователя);
			КонецЕсли;
		КонецЕсли;
	#КонецЕсли
	
КонецПроцедуры

Функция ПолучитьРазрешенныхКлиентовПользователя(Пользователь)
	
	Запрос = Новый запрос("ВЫБРАТЬ
	|	док_РазрешенныеКлиентыПользователейКлиенты.Клиент КАК Клиент
	|ИЗ
	|	Справочник.док_РазрешенныеКлиентыПользователей.Клиенты КАК док_РазрешенныеКлиентыПользователейКлиенты
	|ГДЕ
	|	док_РазрешенныеКлиентыПользователейКлиенты.Ссылка.Пользователь = &Пользователь");
	Запрос.УстановитьПараметр("Пользователь",Пользователь);
	Таб = Запрос.Выполнить().Выгрузить();
	Возврат Таб.ВыгрузитьКолонку("Клиент");
	
КонецФункции	

Функция ИспользоватьОграничениеRLS_ПоКлиентам(Пользователь)
	
	Запрос = Новый запрос("ВЫБРАТЬ
	|	док_РазрешенныеКлиентыПользователей.ИспользоватьОграничение КАК ИспользоватьОграничение
	|ИЗ
	|	Справочник.док_РазрешенныеКлиентыПользователей КАК док_РазрешенныеКлиентыПользователей
	|ГДЕ
	|	док_РазрешенныеКлиентыПользователей.Пользователь = &Пользователь");
	Запрос.УстановитьПараметр("Пользователь",Пользователь);
	ЗВ = Запрос.Выполнить();
	Если ЗВ.Пустой() Тогда
		Возврат Ложь;
	Иначе	
		Таб = ЗВ.Выгрузить();
		Возврат Таб[0].ИспользоватьОграничение;
	КонецЕсли;
	
КонецФункции  
